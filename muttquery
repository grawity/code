#!/usr/bin/env perl
# muttquery - Mutt address book gateway
use common::sense;
use Data::Dumper;

my $query = shift @ARGV;

if (!defined $query) {
	warn "query not specified\n";
	exit 2;
}

my $db = "addressbook";
my $re = qr/$query/i;

my @addressbook = do "$ENV{HOME}/bin/parse-$db.pl";
my @results = ();

for my $entry (@addressbook) {
	my %entry = %$entry;
	my ($name, $info) = ("", "");
	if (exists $entry{email} and grep {defined and /$re/} values %entry) {
		if (defined $entry{name}) {
			$name = $entry{name};
			$info = $entry{nick} // "";
		} else {
			$name = $entry{nick};
		}
		push @results, [$entry{email}, $name, $info];
	}
}

@results = sort {
	my ($A, $B);
	$a->[1] =~ /^(.+) (.+?)$/ and $A = "$2, $1";
	$b->[1] =~ /^(.+) (.+?)$/ and $B = "$2, $1";
	return ($A // $a->[1]) cmp ($B // $b->[1]);
	} @results;

my $count = @results;
print $count." ".($count == 1? "match" : "matches")." for /$query/ in $db\n";

for my $entry (@results) {
	print join("\t", @$entry), "\n";
}
