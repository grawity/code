#!/usr/bin/env perl
# utfvis -- make non-ASCII Unicode characters highly visible

use warnings;
use strict;
use open qw(:std :encoding(UTF-8));
use Getopt::Long qw(:config bundling no_ignore_case);

sub hilight {
	my ($c) = @_;
	$c = sprintf "<U+%04X>", ord($c);
	if (-t 1) {
		return "\e[1;37;41m$c\e[m";
	} else {
		return "$c";
	}
}

my $opt_only = 0;

GetOptions(
	"o|only!" => \$opt_only,
) or exit(2);

while (<>) {
	my $n = s/[^\n\t\x20-\x7e]/hilight($&)/ge;
	if ($opt_only) {
		print "\e[4m$ARGV:$.:\e[m$_" if $n;
	} else {
		print;
	}
} continue {
	# Reset the $. pointer if we're at the end, as
	# otherwise <> doesn't do that.
	close ARGV if eof(ARGV);
}
