#!/usr/bin/env perl
use open qw(:std :utf8);
use strict;
use utf8;
use warnings;
use Data::Dumper;
use Nullroute::Lib;
use Text::CharWidth qw(mbswidth);

my @WEEKDAYS = qw(Se Pr An Tr Ke Pe Še);

my %COLOR = (
	"missed" => "\e[38;5;8m",
	"upcoming" => "\e[1m",
	"+" => "\e[38;5;10m",
	"?" => "\e[38;5;3m",
	"-" => "\e[38;5;9m",
	"reset" => "\e[m",
	"upcoming_bg" => "\e[48;5;234m",
	"reset_bg" => "\e[49m",
);

sub plural {
	my ($int, $str) = @_;
	unless ($int == 1) {
		$str .= "s";
	}
	return $str;
}

sub xplural {
	my ($int, $str) = @_;
	return $int." ".plural($int, $str);
}

sub parse_hours {
	my ($str) = @_;
	my ($h, $m) = map {int} split(/:/, $str);
	return $h * 60 + $m;
}

sub now_hours {
	if ($ENV{PRETEND}) { return parse_hours($ENV{PRETEND}); }
	my @tm = localtime;
	return $tm[2] * 60 + $tm[1];
}

sub fmt_hours {
	my ($int) = @_;
	my ($h, $m) = map {int} $int / 60, $int % 60;
	return sprintf("%02d:%02d", $h, $m);
}

sub now_weekday {
	my @tm = localtime;
	return $WEEKDAYS[$tm[6]];
}

sub load_datafile {
	my ($path) = @_;
	my @data;
	my $fh;

	open($fh, "<", $path)
	|| _die("cannot open '$path': $!");
	while (<$fh>) {
		chomp;
		my ($time, $status, $dest) = split(/\s+/, $_, 3);
		my @days;
		$time = parse_hours($time);
		if ($dest =~ / \[(\S+)\]$/) {
			@days = split(/[, ]/, $1);
			$dest =~ s///;
		}
		push @data, [\@days, $time, $dest, $status];
	}
	close($fh);
	return @data;
}

my $file = $ENV{HOME}."/lib/buses.txt";
my @buses = load_datafile($file);
my $walk_time = 10;

my $now = now_hours();
my $day = now_weekday();
my $next = 0;
my $found = 0;

for my $bus (@buses) {
	my ($days, $_time, $dest, $_status) = @$bus;
	my $status = $_status;
	my $time = fmt_hours($_time);
	my @days = @$days;
	my $upcoming = !$found && $_status ne "-";
	my $delta = $_time - $now;

	_debug("bus: $time $_status <$dest> in $delta minutes");

	if ($delta < $walk_time) {
		if ($delta < -60) { # hide buses older than 1 hour
			_debug(" - skipping, was more than 1 hour ago");
			next;
		}

		if ($delta < 0) {
			_debug(" - missed (left $delta ago)");
		} else {
			_debug(" - missed (leaves in $delta < walk time $walk_time)");
		}

		if ($delta < -60) {
			_debug(" - hiding (was long ago)");
			next;
		}

		$status = "missed";
		$upcoming = 0;
	}

	if ($delta > 60*5) { # hide buses later than 5 hours
		_debug(" - skipping, too late");
		next;
	}

	if (@days && !grep {$_ eq $day} @days) {
		_debug(" - skipping, no weekdays ($day vs {@days})");
		next;
	}
	
	_debug(" - status '$status', upcoming ".int($upcoming));

	print
		$upcoming ? "→" : " ",
		" ",
		$upcoming ? $COLOR{"upcoming"} : "",
		$status eq "-" ? "\e[2m" : "",
		$COLOR{$status},
		$time,
		$COLOR{"reset"},
		"  ",
		$upcoming ? $COLOR{"upcoming"} : "",
		$status eq "-" ? "\e[2m" : "",
		$dest,
		"\e[m",
		" "x(20-mbswidth($dest)),
		$upcoming ? $COLOR{"upcoming"} : "",
		"\e[2m",
		$delta > 0 ? interval($delta*60, 0) : "",
		$COLOR{"reset"},
		"\n";

	if ($status ne "-" && $delta > 0) {
		$next ||= $_time;
	}

	if ($status eq "+" && $delta > $walk_time) {
		$found ||= $_time;
	}
}

_debug("=> results: next '".fmt_hours($next)."', found '".fmt_hours($found)."'");

if ($next) {
	for (grep {$_ > 0} uniq $next, $found) {
		my $delta = $_ - $now;
		print
			"\n",
			$delta < 10 ? "\e[1;33m" : "",
			"  You have ",
			xplural($delta, "minute"),
			" until the ",
			$COLOR{"upcoming_bg"},
			" ",
			fmt_hours($_),
			" ",
			$COLOR{"reset_bg"},
			" bus.",
			$COLOR{"reset"},
			"\n";
	}
}
