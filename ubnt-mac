#!/usr/bin/env perl
use Nullroute::Lib;

sub parse {
	my ($mac) = @_;

	my $hex = qr/[0-9a-f]{2}/i;
	my @formats = (
		qr/^($hex):($hex):($hex):($hex):($hex):($hex)$/,
		qr/^($hex)-($hex)-($hex)-($hex)-($hex)-($hex)$/,
		qr/^($hex)($hex)\.($hex)($hex)\.($hex)($hex)$/,
		qr/^($hex)($hex)($hex)($hex)($hex)($hex)$/,
	);

	for my $re (@formats) {
		_debug("testing $re");
		if (my @m = $mac =~ $re) {
			return map {hex} @m;
		}
	}
}

sub unparse {
	join(":", map {sprintf "%02x", $_} @_);
}

my $mac = shift @ARGV;
if (!$mac) {
	_die("missing MAC address");
}

my @mac = parse($mac);
if (@mac != 6) {
	_die("invalid MAC address");
}

$mac = unparse(@mac);

my $kind = ($mac[3] & 1) ? "WiFi" : "Wired";

my @wifi_mac = @mac;
@wifi_mac[3] |= 1;
my $wifi_mac = unparse(@wifi_mac);

my @wired_mac = @mac;
@wired_mac[3] &= ~1;
my $wired_mac = unparse(@wired_mac);

print "$mac is the $kind address\n";
print " * Wired: $wired_mac\n";
print " * WiFi:  $wifi_mac\n";
