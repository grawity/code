#!/usr/bin/env perl
# show-vcard - vCard info viewer
use warnings;
use strict;
use feature 'say';
use Data::Dumper;
use Text::vCard::Addressbook;

my @files = @ARGV;
if (!@files) {
	push @files, "/dev/stdin";
}

my $book = Text::vCard::Addressbook->load(\@files);

sub get_type {
	my ($node) = @_;
	do { return $_ if $node->is_type($_) } for qw(home work cell);
	return "other";
}

for my $card ($book->vcards) {
	say "Name: ", $card->fullname;

	my $emails = $card->get({node_type => "EMAIL"});
	for my $addr (@$emails) {
		say "Mail: ", "<".$addr->value.">";
	}

	my $addresses = $card->get({node_type => "addresses"});
	for my $addr (@$addresses) {
		say "Address (".get_type($addr)."):";
		say "  ", $addr->street;
		say "  ", join(", ", grep {defined} $addr->city, $addr->region);
		say "  ", $addr->post_code, " ", $addr->country;
	}

	my $phones = $card->get({node_type => "tel"});
	for my $tel (@$phones) {
		say "Phone: ".$tel->value." (".get_type($tel).")";
	}
	
	my $urls = $card->get({node_type => "URL"});
	for my $url (@$urls) {
		say "Web: ".$url->value;
	}

	say "";
}

