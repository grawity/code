#!/usr/bin/env perl
# utfvis -- make non-ASCII Unicode characters highly visible

use warnings;
use strict;
use open qw(:std :encoding(UTF-8));
use Getopt::Long qw(:config bundling no_ignore_case);

sub usage {
	print for
	"Usage: utfvis [-o] <file>...\n",
	"\n",
	"Options:\n",
	"  -o, --only    show only lines containing non-ASCII characters\n";
}

sub hilight {
	my ($char) = @_;
	$char = sprintf "<U+%04X>", ord($char);
	if (-t 1) {
		return "\e[1;37;41m$char\e[m";
	} else {
		return $char;
	}
}

my $opt_only = 0;

GetOptions(
	"o|only!" => \$opt_only,
	"help" => sub { usage(); exit(0); },
) or exit(2);

while (<>) {
	my $n = s/[^\n\t\x20-\x7e]/hilight($&)/ge;
	if ($opt_only) {
		print "\e[4m$ARGV:$.:\e[m$_" if $n;
	} else {
		print;
	}
} continue {
	# Reset $. between files, as <> doesn't do that automatically.
	close ARGV if eof(ARGV);
}
