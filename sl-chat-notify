#!/usr/bin/env perl
# sl-chat-notify -- SecondLife chat log to libnotify filter
use warnings;
use Net::DBus;
use POSIX;

$ENV{TZ} = "America/Los_Angeles";

sub Notifications {
	Net::DBus
	->session
	->get_service("org.freedesktop.Notifications")
	->get_object("/org/freedesktop/Notifications")
}

sub parse_date {
	my $str = shift;
	$str =~ s/^\[//;
	$str =~ s/\]$//;
	my ($dy, $dm, $dd, $th, $tm) = split(m![/ :]+0?!, $str);
	POSIX::mktime(0, $tm, $th, $dd, $dm-1, $dy-1900);
}

my $title = shift @ARGV;

my $date_re = qr!\[\d+/\d+/\d+ \d+:\d+\]!;
my $name_re = qr![^:]+:?!;

my @times = ();
my @names = ();
my @texts = ();

my $prev_time;
my $prev_name;
my $prev_text;

my $id = 0;

sub fname { "—— ".shift()." ——"; }

sub do_msg {
	my ($time, $name, $text) = @_;

	if ($name eq "Second Life") {
		return if $text =~ /^Teleport completed/;
		$text =~ s/^Now playing:/♫/;
	}

	# fix name
	if ($name eq 'IM' && $text =~ /^($name_re): /) {
		$name = "(IM) $1";
		$text =~ s///;
	}
	if ($name =~ / \(.+?\)$/) {
		$name =~ s///;
	}

	# fix text
	$text =~ s/\s+$//;
	$text =~ s!^/me('.+)!($name$1)!
	or $text =~ s!^/me (.+)!($1)!
	or $text =~ s!^whispers: (.+)$!(whispers: "$1")!;

	# display
	print fname($name)."\n".$text."\n";

	push @times, $prev_time = $time;
	push @names, $prev_name = $name;
	push @texts, $prev_text = $text;

	if (@texts > 4 || (time - $times[0]) > 90) {
		shift @times;
		shift @names;
		shift @texts;
	}

	if (@texts) {
		my @lines;
		my $prev_name = "";
		for my $i (0..$#texts) {
			#my ($name, $text, $time) = @{$last[$i]};
			push @lines, fname($names[$i])
				if $names[$i] ne $prev_name;
			$prev_name = $names[$i];
			push @lines, $texts[$i];
		}
		$id = Notifications->Notify(
				"Second Life",
				$id,
				"",
				"nearby chat ($title)",
				join("\n", @lines),
				[],
				{},
				-1);
	}
}

while (<>) {
	if (/^($date_re)  ($name_re): (.+)$/) {
		$time = parse_date($1);
		$name = $2;
		$text = $3;
		do_msg($time, $name, $text);
	}
	elsif (/^ (.+)$/) {
		if ($prev_time) {
			$time = $prev_time;
			$name = $prev_name;
			$text = $1;
			do_msg($time, $name, $text);
		}
	}
}
