#!/usr/bin/env perl
# trash - move files into XDG Trash
use v5.10;
use warnings;
use strict;
use Cwd qw(realpath);
use Errno qw(:POSIX);
use Fcntl;
use File::Basename qw(basename dirname);
use File::Path qw(make_path);
use File::Spec::Functions;
use Getopt::Long qw(:config bundling no_ignore_case);
use Nullroute::Lib;
use POSIX qw(strftime);

my $verbose = 1;
my $interactive = 0;

sub confirm {
	print "$::arg0: @_ "; $|++; <STDIN> =~ /^y/i;
}

sub dev {
	(lstat(shift))[0];
}

=item my_abs_path($path)

Canonicalize symlinks and relative paths.
If $path itself is a symlink, do not canonicalize it.

=cut

sub my_abs_path {
	my ($path) = @_;
	my $b = basename($path);
	my $d = dirname($path);
	my $rd = realpath($d);
	_debug("abs: dir='$d' realdir='$rd' base='$b'");
	return $rd."/".$b;
}

=item find_root($abs_path)

Find the root directory of the filesystem $path is in.

Does not currently work with bind-mounted files; returns the mountpoint's parent.

=cut

sub find_root {
	my ($path) = @_;
	my $fdev = dev($path);
	return undef if !defined $fdev;
	my $prev = $path;
	while ($path ne "/") {
		$prev = $path;
		$path = dirname($path);
		return $prev if dev($path) != $fdev;
	}
	return $path;
}

=item ensure($trash_dir)

Recursively mkdir $trash_dir/{files,info} if necessary.

=cut

sub ensure {
	my ($trash_dir) = @_;
	for ($trash_dir, "$trash_dir/info", "$trash_dir/files") {
		next     if -d $_;
		return 0 if -e $_;
		make_path($_, {mode => 0700}) or return 0;
	}
	return 1;
}

=item xdev_move($source, $dest) -> $success

Copy a file or directory $source to $dest recursively and delete the originals.

=cut

sub xdev_move {
	my ($source, $dest) = @_;
	my @opt;
	_debug("xdev_move: source='$source'");
	_debug("xdev_move: dest='$dest'");

	_info("copying '$source' to \$HOME...");
	@opt = qw(-a -H -A -X);
	if ($::debug) { push @opt, qw(-v -h); }
	system("rsync", @opt, "$source", "$dest") == 0
		or return 0;

	_info("removing '$source' after copying...");
	@opt = qw(-r -f);
	if ($::debug) { push @opt, qw(-v); }
	system("rm", @opt, $source) == 0
		or return 0;

	return 1;
}

=item create_info($trash_dir, $orig_path) -> ($name, $fh, $path)

Securely create a .trashinfo file in $trash_dir with a basename similar
to that of $orig_path; return the new basename, a writable filehandle,
and for convenience the full path to the file.

=cut

sub create_info {
	my ($trash_dir, $orig_path) = @_;
	my $base = basename($orig_path);
	my $i = 0;
	my ($name, $fh, $info_path);
	while ($i < 1000) {
		$name = $i ? "$base-$i" : $base;
		$info_path = "$trash_dir/info/$name.trashinfo";
		if (sysopen($fh, $info_path, O_WRONLY|O_CREAT|O_EXCL)) {
			_debug("found free info_path='$info_path'");
			return ($name, $fh, $info_path);
		} elsif ($! == EEXIST) {
			_debug("'$name.trashinfo' already exists, trying next...")
				if ($i % 25 == 0);
		} else {
			_err("cannot create '$info_path' ($!)");
			return undef;
		}
		++$i;
	}
	_debug("giving up after $i failures");
	_err("cannot create .trashinfo file (too many items named '$base')");
	return undef;
}

=item write_info($fh, $orig_path)

Write the [Trash Info] block for $orig_path to a filehandle.

=cut

sub write_info {
	my ($info_fh, $orig_path) = @_;

	my $now = strftime("%Y-%m-%dT%H:%M:%S%z", localtime);

	print $info_fh "[Trash Info]\n";
	print $info_fh "Path=$orig_path\n";
	print $info_fh "DeletionDate=$now\n";
}

=item pase_date($iso8601_str)

=cut

sub parse_date {
	my ($str) = @_;

	my $r = qr/^
		(\d+) - 0*(\d+) - 0*(\d+)
		T 0*(\d+) : 0*(\d+) (?: : 0*(\d+) )?
		(?: \+ \d{4} )?
		$/x;

	if (my @m = $str =~ $r) {
		my ($dy, $dm, $dd, $th, $tm, $ts, $tz) = map {int($_ // 0)} @m;
		return POSIX::mktime($ts, $tm, $th, $dd, $dm-1, $dy-1900);
	} else {
		return 0;
	}
}

=item read_info($file)

=cut

sub read_info {
	my ($info_path) = @_;

	my ($info_fh, $orig_path, $trash_time);
	if (!open($info_fh, "<", $info_path)) {
		_err("could not read \"$info_path\": $!");
		return;
	}
	for (<$info_fh>) {
		if ($. == 1 && !/^\[Trash Info\]$/) {
			_err("syntax error in \"$info_path\"");
			last;
		} elsif (!defined($orig_path) && /^Path=(.+)$/) {
			$orig_path = $1;
		} elsif (!defined($trash_time) && /^DeletionDate=(.+)$/) {
			$trash_time = parse_date($1);
		}
	}
	close($info_fh);
	return ($orig_path, $trash_time);
}

sub find_unique {
	my ($base) = @_;
	my $path = $base;
	my $i = 1;
	while (-e $path) {
		$path = $base."-".$i++;
		_die("could not find unique name for \"$base\"") if $i > 1_000;
	}
	return $path;
}

sub restore_from_info {
	my ($info_path, $orig_path) = @_;
	my $trash_dir = dirname(dirname($info_path));
	my $info_base = basename($info_path, ".trashinfo");
	my $trashed_path = "$trash_dir/files/$info_base";
	my $orig_dir = dirname($orig_path);
	$orig_path = find_unique($orig_path);
	if (dev($orig_dir) == dev($trash_dir)) {
		if (rename($trashed_path, $orig_path)) {
			unlink($info_path);
			_info("restored '$orig_path'");
		} else {
			_die("failed to rename '$trashed_path': $!");
		}
	} else {
		if (xdev_move($trashed_path, $orig_path)) {
			unlink($info_path);
			_info("restored '$orig_path'");
		} else {
			_die("failed to copy '$trashed_path' to '$orig_dir'");
		}
	}
}

=item find_trash_dir($orig_path)

Find the best trash directory to use, according to XDG Trash Dir spec.

 * $home_trash if same device
 * $root/.Trash/$UID if checks pass
 * $root/.Trash-$UID if exists or can create
 * $home_trash otherwise

=cut

sub find_trash_dir {
	my ($orig_path) = @_;

	my $home_trash = ($ENV{XDG_DATA_HOME} // $ENV{HOME}."/.local/share") . "/Trash";

	ensure($home_trash);

	_debug("trying to find trash for path='$orig_path'");
	my $fdev = dev($orig_path);
	while (!defined $fdev) {
		$orig_path = dirname($orig_path);
		_debug("...path not found, using parent='$orig_path'");
		$fdev = dev($orig_path);
	}

	my $hdev = dev($home_trash);
	if (!defined $fdev) {
		return undef;
	} elsif ($fdev == $hdev) {
		return $home_trash;
	} else {
		my $root = find_root($orig_path);
		my $dir = catdir($root, ".Trash");
		if (-d $dir && ! -l $dir && -k $dir && ensure("$dir/$<")) {
			return "$dir/$<";
		}
		$dir = catdir($root, ".Trash-$<");
		if (-d $dir || ensure($dir)) {
			return $dir;
		}
	}
	return $home_trash;
}

=item trash($path)

Create a trashinfo file in the appropriate trash directory, then move
actual $path there. If move fails, delete trashinfo and explode.

=cut

sub trash {
	my ($path) = @_;
	if (!lstat($path)) {
		_err("not found: '$path'");
		return;
	}
	if ($interactive) {
		confirm("Kill file <$path>?") || return;
	}

	my $orig_path = my_abs_path($path);
	_debug("orig_path='$orig_path'");

	my $orig_dir = dirname($orig_path);
	if (!-w $orig_dir) {
		_err("no write permissions to parent directory '$orig_dir'");
	}

	my $trash_dir = find_trash_dir($orig_path);
	_debug("trash_dir='$trash_dir'");
	ensure($trash_dir);

	my ($name, $info_fh, $info_name) = create_info($trash_dir, $orig_path);
	if (!$info_fh) {
		_err("failed to move '$path' to trash");
		return;
	}
	write_info($info_fh, $orig_path);

	my $trashed_path = "$trash_dir/files/$name";
	if (dev($orig_path) == dev($trash_dir)) {
		_debug("same-device renaming to '$trashed_path'");
		if (rename($orig_path, $trashed_path)) {
			_info("trashed '$path'");
		} else {
			_debug("rename failed, unlinking info '$info_name'");
			unlink($info_name);
			_die("failed to rename '$path': $!");
		}
	} else {
		_debug("cross-device moving to '$trashed_path'");
		if (xdev_move($orig_path, $trashed_path)) {
			_info("trashed '$path' to \$HOME");
		} else {
			_debug("xdev_move failed, unlinking info '$info_name'");
			unlink($info_name);
			_die("failed to copy '$path' to '$trash_dir'");
		}
	}
	close($info_fh);
}

sub untrash {
	my ($path) = @_;
	my $orig_path = my_abs_path($path);
	_debug("orig_path='$orig_path'");
	my $orig_dir = dirname($orig_path);
	_debug("orig_dir='$orig_dir'");
	my $trash_dir = find_trash_dir($path);
	_debug("trash_dir='$trash_dir'");
	if (!-d "$trash_dir/info" || !-d "$trash_dir/files") {
		_err("trash directory does not exist");
	}
	my @infos;
	for my $info_path (glob("$trash_dir/info/*.trashinfo")) {
		my ($info_orig_path, $info_trash_date) = read_info($info_path);
		if (-d $orig_path) {
			my $info_orig_dir = dirname($info_orig_path);
			next if $orig_dir ne $info_orig_dir;
		} else {
			next if $orig_path ne $info_orig_path;
		}
		_debug("found \"$info_orig_path\" time $info_trash_date");
		push @infos, [$info_path, $info_orig_path, $info_trash_date];
	}
	@infos = sort {$a->[2] <=> $b->[2]} @infos;
	if (!@infos) {
		_err("no matching files in trash");
		return;
	} elsif (@infos == 1) {
		my $info = $infos[0];
		my $age = interval($info->[2]);
		_info("found one matching file ($age ago), restoring");
		restore_from_info(@$info);
	} else {
		my $info = pop @infos;
		my $age = interval($info->[2]);
		_info("found ".@infos." matching files in trash, restoring most recent");
		restore_from_info(@$info);
	}
}

sub usage {
	say for
	"Usage: $::arg0 [options] <file>...",
	"",                       #
	"  -f, --force            Ignored (compatibility with `rm`)",
	"  -i, --[no-]interactive Prompt before removing a file",
	"  -r, --recursive        Ignored (compatibility with `rm`)",
	"  -v, --[no-]verbose     Show files being removed",
	"",
	"      --find-trash       Only print the trash directory",
	"      --restore          Restore files from trash";
}

# Option parser

my $print_path = 0;
my $restore = 0;

GetOptions(
	'help'		=> sub { usage(); exit; },
	'find-trash!'	=> \$print_path,
	'f|force'	=> sub { },
	'i|interactive!'=> \$interactive,
	'restore!'	=> \$restore,
	'r|R|recursive'	=> sub { },
	'v|verbose!'	=> \$verbose,
) or exit 2;

if (!@ARGV) {
	_die("no files given");
}

# Main code

if ($print_path) {
	say find_trash_dir(my_abs_path($_)) // "(not found?)" for @ARGV;
} elsif ($restore) {
	untrash($_) for @ARGV;
} else {
	trash($_) for @ARGV;
}

_exit();
