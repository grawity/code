#!/usr/bin/env perl
# ctl - write one-line strings to files
#   To be used like `sysctl`, just for things outside /proc/sys.
#   For example, ctl /sys/modules/snd_hda_intel/parameters/power_save=1
use warnings;
use strict;

my $err;
my $value;

sub err($) { warn @_; ++$err; }

sub print_file_data {
	my ($file, $data) = @_;
	$data =~ s/\0/\\0/g;
	$data =~ s/\n/\\n/g;
	$data =~ s/\t/\\t/g;
	$data =~ s/[\x00-\x1F\x80-\xFF]/sprintf("\\%03o", ord $&)/ge;
	print "$file = $data\n";
}

sub write_ctl {
	my ($file, $data) = @_;

	if (my $fh = IO::File->new($file, "w")) {
		$fh->print($data."\n");
		if ($fh->flush()) {
			print_file_data($file, $data);
		} else {
			err "ctl: file '$file': $!\n";
		}
		$fh->close();
	} else {
		err "ctl: file '$file': $!\n";
	}
}

sub read_ctl {
	my ($file, $depth) = @_;

	if (-d $file && -l $file) {
		warn "ctl: skipping symlink '$file'\n";
	} elsif (-d $file) {
		$file =~ s|/+$||;
		if ($depth > 10) {
			warn "ctl: stopping descent into '$file/*'\n";
			return;
		}
		read_ctl($_, $depth+1) for glob("$file/*");
	} elsif (-f $file) {
		if (my $fh = IO::File->new($file, "r")) {
			my $data;
			if ($fh->read($data, 512)) {
				chomp $data;
				print_file_data($file, $data);
			} elsif ($!) {
				print "$file: error ($!)\n";
			} else {
				print "$file: empty\n";
			}
			$fh->close();
		} else {
			err "ctl: file '$file': $!\n";
		}
	} elsif (-e $file || -l $file) {
		warn "ctl: skipping non-regular file '$file'\n";
	} else {
		err "ctl: no such file '$file'\n";
	}
}

push @ARGV, "." if !@ARGV;

for (@ARGV) {
	if (/^([^=]+)=(.*)$/) {
		write_ctl($1, $2);
	} elsif (/^=(.*)$/) {
		$value = $1;
	} elsif (defined $value) {
		write_ctl($_, $value);
	} else {
		read_ctl($_, 0);
	}
}

exit !!$err;
