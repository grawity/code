#!/usr/bin/env perl
# fqdn -- Retrieve a hostname's fully qualified name
#
# Uses forward lookups via AI_CANONNAME (like most tools do). Does not use
# reverse DNS, although that should be added as an option someday.

use warnings;
use strict;
use Getopt::Long;
use Socket qw(AI_CANONNAME getaddrinfo);
use Sys::Hostname;

sub usage {
	print for
		"Usage: $0 [-s suffixes] [-v] <domain>...\n",
		"\n",
		"Options:\n",
		"\n",
		"  -s suffixes   perform a suffix search instead\n",
		"  -v            verbose mode\n",
		"\n",
		#12345678........12345678........12345678........12345678........12345678........
		"In default mode, the given name is looked up using system DNS search suffixes\n",
		"and the alias chain is followed to find the canonnical name (like getaddrinfo\n",
		"with AI_CANONNAME).\n",
		"\n",
		"In suffix search mode, the given name is looked up using only the specified\n",
		"suffixes, and aliases are NOT followed; each name is merely tested to see if it\n",
		"resolves at all.\n";
}

sub joinhost {
	my ($domain, $suffix) = @_;

	return $domain if $domain =~ /\.$/;
	return $domain if $suffix eq "";
	return $domain if $suffix eq ".";
	return $domain.".".$suffix;
}

sub get_fqdn_from_suffix {
	my ($host, $suffixes, $v) = @_;

	for my $suffix (@$suffixes, "") {
		my $fqdn = joinhost($host, $suffix);
		warn "fqdn: trying to resolve '$fqdn'\n" if $v;
		my $hints = {flags => AI_CANONNAME};
		my ($err, @res) = getaddrinfo($fqdn, undef, $hints);
		if (@res && !$err) {
			# Return $fqdn instead of canonname, because in
			# this case it is not our goal to chase CNAMEs,
			# only to discover the correct domain suffix.
			return $fqdn;
		}
	}
	warn "fqdn: lookup of '$host' ran out of suffixes\n" if $v;
	return $host;
}

sub get_fqdn_from_cnames {
	my ($host, $v) = @_;

	my $hints = {flags => AI_CANONNAME};
	my ($err, @res) = getaddrinfo($host, undef, $hints);
	warn "fqdn: could not resolve '$host': $err\n" if $err;
	return $res[0]->{canonname} if @res;
	warn "fqdn: lookup of '$host' failed with no suffixes given\n" if $v;
	return $host;
}

my $suffixes = $ENV{LOCALDOMAIN};
my $verbose = 0;

GetOptions(
	"d|s|search=s" => \$suffixes,
	"v|verbose" => \$verbose,
	"help" => sub { usage(); exit(0); },
) || exit(2);

my @suffixes = split(/[:, ]/, $suffixes // "");

my @args = @ARGV ? @ARGV : hostname();
my $errs = 0;

for my $arg (@args) {
	my $res;

	if (@suffixes) {
		$res = get_fqdn_from_suffix($arg, \@suffixes, $verbose);
	} else {
		$res = get_fqdn_from_cnames($arg, $verbose);
	}

	if ($res) {
		print "$res\n";
	} else {
		print "$arg\n"; ++$errs;
	}
}

exit(!!$errs);
